PREFIX nc: <https://cim4.eu/ns/nc#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX cimr: <https://cim.ucaiug.io/rules#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cim: <https://cim.ucaiug.io/ns#>
PREFIX uml: <http://iec.ch/TC57/NonStandard/UML#>
PREFIX cims: <http://iec.ch/TC57/1999/rdf-schema-extensions-19990926#>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX eu: <https://cim.ucaiug.io/ns/eu#>
PREFIX onto: <http://www.ontotext.com/>

CONSTRUCT {
    ?x ?p ?o .
}
# extracting only explicit statements for the ontology subset
FROM onto:explicit 
WHERE {
    {
        # using federation to self to gather classes and properties 
        # used in both explict and inferred statements
        SERVICE <repository:nordic44> {           
        	SELECT 
            DISTINCT ?x 
            {
                {
                    [] ?x []
                } UNION {
                    [] a ?x
                }
        	}
        }
    }
    UNION {
        ?x rdfs:isDefinedBy cimr:
    }
    UNION {
        # using federation to self to gather enumerated values 
        # from both explicit and inferred statements
        SERVICE <repository:nordic44> {
            SELECT * {
                ?x a ?enum.
                ?enum cims:stereotype uml:enumeration
                FILTER EXISTS {
                    ?thing a cim:IdentifiedObject;
                    ?prop ?x
                }
            }
        }
    }
    ?x ?p ?o .
    filter(?p not in (cims:belongsToCategory))
    filter(?x not in (cims:belongsToCategory))  
}
