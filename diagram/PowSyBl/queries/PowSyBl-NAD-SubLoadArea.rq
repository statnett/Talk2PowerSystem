PREFIX cimd: <https://cim.ucaiug.io/diagrams#> 
PREFIX cim:  <https://cim.ucaiug.io/ns#>
PREFIX afn:  <http://jena.apache.org/ARQ/function#>
PREFIX dct:  <http://purl.org/dc/terms/>

construct {
  ?diag a cimd:Diagram; cimd:Diagram.kind cimd:DiagramKind.PowSyBl-NetworkAreaDiagram;
    cim:IdentifiedObject.name ?diagName; cim:IdentifiedObject.mRID ?diagMrid; cim:IdentifiedObject.description ?diagDescr;
    cimd:Diagram.PowerSystemResource ?psr; cimd:Diagram.mRIDs ?mrids; cimd:Diagram.link ?link; dct:format "image/svg+xml"
}
where {
  {select ?psr (concat('(',group_concat(?quotedMrid; separator=","),')') as ?mrids) {
    ?psr a cim:SubLoadArea.
    ?voltageLevel a cim:VoltageLevel;
      cim:EquipmentContainer.Equipments/(cim:ConformLoad.LoadGroup|cim:NonConformLoad.LoadGroup)/cim:LoadGroup.SubLoadArea ?psr;
      cim:IdentifiedObject.mRID ?mrid.
    bind(concat('"',?mrid,'"') as ?quotedMrid)
  } group by ?psr}
  ?psr cim:IdentifiedObject.name ?psrName; cim:IdentifiedObject.mRID ?psrMrid.
  bind(replace(?psrName," ","-") as ?psrSlug)
  bind(uuid() as ?diag)
  bind(afn:localname(?diag) as ?diagMrid)
  bind(concat("Diagram of SubLoadArea ",?psrName) as ?diagName)
  bind(concat("PowSyBl Network-Area-Diagram of voltage levels in SubLoadArea ",?psrName) as ?diagDescr)
  bind(concat("PowSyBl-NAD-SubLoadArea-",?psrSlug,".svg") as ?link)
}
