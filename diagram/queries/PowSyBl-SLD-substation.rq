PREFIX cimd: <https://cim.ucaiug.io/diagrams#>
PREFIX cim:  <https://cim.ucaiug.io/ns#>
PREFIX afn:  <http://jena.apache.org/ARQ/function#>
PREFIX dct:  <http://purl.org/dc/terms/>

construct {
  ?diag a cimd:Diagram; cimd:Diagram.kind cimd:DiagramKind.PowSyBl-SingleLineDiagram;
    cim:IdentifiedObject.name ?diagName; cim:IdentifiedObject.mRID ?diagMrid; cim:IdentifiedObject.description ?diagDescr;
    cimd:Diagram.PowerSystemResource ?psr; cimd:Diagram.mRIDs ?mrids; cimd:Diagram.link ?link; dct:format "image/svg+xml"
} 
where {
  {select * {?psr a cim:Substation; cim:IdentifiedObject.name ?psrName; cim:IdentifiedObject.mRID ?psrMrid}}
  bind(uuid() as ?diag)
  bind(afn:localname(?diag) as ?diagMrid)
  bind(replace(?psrName," ","-") as ?psrSlug)
  bind(concat("Diagram of substation ",?psrName) as ?diagName)
  bind(concat("PowSyBl Single-Line-Diagram of substation ",?psrName) as ?diagDescr)
  bind(concat('"',?psrMrid,'"') as ?mrids)
  bind(concat("PowSyBl-SLD-substation-",?psrSlug,".svg") as ?link)
}
