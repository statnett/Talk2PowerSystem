prefix xyz:  <http://sparql.xyz/facade-x/data/>
prefix fx:   <http://sparql.xyz/facade-x/ns/>
prefix cimd: <https://cim.ucaiug.io/diagrams#> 
prefix cim:  <https://cim.ucaiug.io/ns#>
prefix cimr: <https://cim.ucaiug.io/rules#>
prefix nc:   <https://cim4.eu/ns/nc#>
prefix dct:  <http://purl.org/dc/terms/> 

construct {
   ?uri a cimd:Diagram ;
        cim:IdentifiedObject.name ?name ;
        cimd:Diagram.link ?link ;
        cimd:Diagram.PowerSystemResource ?psr ;
        cimd:Diagram.kind cimd:DiagramKind.GraphDB-VizGraph ;
        dct:format "text/html" ;
    .
} where {
  service <x-sparql-anything:> {
    fx:properties fx:location $_location .
    [] xyz:data ?data ;
       xyz:id   ?id ;
       xyz:name ?name ;
    .
    bind(concat("graphs-visualizations?saved=",?id) as ?link)
    bind(uuid() as ?uri) 
    service <x-sparql-anything:> {
      fx:properties 
        fx:content ?data ;
        fx:media-type  "application/json" .
        [] xyz:iri ?iri .       
    } 
  }
  bind(uri(?iri) as ?psr)      
}  
